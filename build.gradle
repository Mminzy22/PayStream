// 플러그인을 중앙에서 버전만 관리하고, 실제 적용은 하위 모듈에서 하도록 설정
plugins {
    id 'org.springframework.boot' version '3.5.6' apply false
    id 'io.spring.dependency-management' version '1.1.7' apply false
    id 'java'
}

// 모든 하위 프로젝트에 '공통으로' 적용될 설정
allprojects { // 'subprojects' 대신 'allprojects'를 사용하면 루트 프로젝트에도 적용되어 일관성 있음
    group = 'com.paystream'
    version = '0.0.1-SNAPSHOT'

    repositories {
        mavenCentral()
    }
}

// 루트를 제외한 모든 '하위' 프로젝트에 적용될 설정
subprojects {
    apply plugin: 'java'
    apply plugin: 'io.spring.dependency-management'

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(17)
        }
    }

    // Spring Cloud 버전 변수 설정
    ext {
        springBootVersion = '3.5.6'
        springCloudVersion = "2025.0.0"
        lombokVersion = "1.18.30"
        mockitoVersion = "5.12.0"
    }

    // Spring Cloud BOM 적용
    dependencyManagement {
        imports {
            mavenBom "org.springframework.boot:spring-boot-dependencies:${springBootVersion}"
            mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
        }
    }

    dependencies {
        // Lombok 설정
        compileOnly "org.projectlombok:lombok:${lombokVersion}"
        annotationProcessor "org.projectlombok:lombok:${lombokVersion}"
        testCompileOnly "org.projectlombok:lombok:${lombokVersion}"
        testAnnotationProcessor "org.projectlombok:lombok:${lombokVersion}"
    }

    // 모든 하위 프로젝트에서 JUnit5 기반 테스트를 사용하도록 설정
    test {
        useJUnitPlatform()
    }
}